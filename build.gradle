plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.diffplug.spotless" version "5.15.0"
    id "org.owasp.dependencycheck" version "6.3.1"
    id "org.flywaydb.flyway" version "8.0.0-beta1"
}

// Configuration for the dependency management plugin
// https://github.com/spring-gradle-plugins/dependency-management-plugin
dependencyManagement {
    imports {
        mavenBom 'org.springframework:spring-framework-bom:5.3.9'
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.5.4'
        mavenBom 'org.junit:junit-bom:5.7.2'
    }
}

group = 'br.com.template'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
	environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "local"
}

test {
	useJUnitPlatform()
}

// Configuration for the spotless plugin
// https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    encoding 'UTF-8'
	compileJava.dependsOn 'spotlessApply'
	format 'misc', {
		target '**/*.md', '**/.gitignore', '**/*.yml', '**/*.yaml', '**/*.json'
		targetExclude 'build/**'
		trimTrailingWhitespace()
		endWithNewline()
	}
	java {
		importOrderFile 'spotless.importorder'
		targetExclude 'build/generated-sources/**'
		removeUnusedImports()
		eclipse().configFile 'spotless.eclipse-fmt.xml'
        endWithNewline()
        trimTrailingWhitespace()
		custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
	}
	lineEndings 'UNIX'
}

flyway {
    url = 'jdbc:h2:file:~/crudtest'
    user = 'SA'
    password =''
    //schemas = ['public']
    createSchemas=true
}
